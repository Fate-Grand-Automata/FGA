apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 30
    ndkVersion "21.3.6528147"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    kotlinOptions {
        useIR = true

        freeCompilerArgs += "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }

    testOptions {
        // This is not a good idea
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "com.mathewsachin.fategrandautomata"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode Integer.valueOf(System.env.FGA_VERSION_CODE ?: 1)
        versionName(System.env.FGA_VERSION_NAME ?: "0.1.0")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    signingConfigs {
        debug {
            storeFile file("fgadebug.keystore")
            storePassword "123abc"
            keyAlias "fgadebug"
            keyPassword "123abc"
        }
        release {
            storeFile file("fgautomata.keystore")
            storePassword System.env.KEYSTORE_PASS
            keyAlias "fgautomata"
            keyPassword System.env.KEYSTORE_PASS
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        ci {
            initWith release
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libautomata')
    implementation project(':scripts')
    implementation project(':prefs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "androidx.activity:activity-ktx:1.3.0-rc01"
    implementation "androidx.fragment:fragment-ktx:1.3.5"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.github.iamareebjamal:opencv-android:4.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // ViewModel, LiveData
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation "io.mockk:mockk:1.10.3"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'

    implementation 'com.jakewharton.timber:timber-android:5.0.0-SNAPSHOT'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    implementation 'androidx.activity:activity-compose:1.3.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha04"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'

    implementation "com.google.accompanist:accompanist-insets:0.12.0"
}

kapt {
    correctErrorTypes true
}
