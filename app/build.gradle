apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 33
    ndkVersion "21.3.6528147"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    kotlinOptions {
        freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.4.8"
    }


    defaultConfig {
        applicationId "io.github.fate_grand_automata"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode Integer.valueOf(System.env.FGA_VERSION_CODE ?: 1)
        versionName(System.env.FGA_VERSION_NAME ?: System.env.FGA_VERSION_CODE ?: "0.1.0")

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    signingConfigs {
        debug {
            storeFile file("fgadebug.keystore")
            storePassword "123abc"
            keyAlias "fgadebug"
            keyPassword "123abc"
        }
        release {
            storeFile file("fgautomata.keystore")
            storePassword System.env.KEYSTORE_PASS
            keyAlias "fgautomata"
            keyPassword System.env.KEYSTORE_PASS
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        ci {
            initWith release
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }
    lint {
        abortOnError false
    }
    namespace 'io.github.fate_grand_automata'
}

dependencies {
    implementation project(':libautomata')
    implementation project(':scripts')
    implementation project(':prefs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    def activity_version = "1.7.2"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.iamareebjamal:opencv-android:4.5.1'

    // ViewModel, LiveData
    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    implementation "com.google.code.gson:gson:$gson_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'

    def composeBom = platform('androidx.compose:compose-bom:2023.06.01')
    implementation composeBom
    androidTestImplementation composeBom
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"

    implementation "androidx.activity:activity-compose:$activity_version"
    implementation "androidx.navigation:navigation-compose:2.6.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    def accompanist_version = "0.30.1"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    implementation "com.google.android.play:app-update-ktx:2.1.0"

}

kapt {
    correctErrorTypes true
}
