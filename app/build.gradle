apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    ndkVersion "21.3.6528147"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }

    testOptions {
        // This is not a good idea
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "com.mathewsachin.fategrandautomata"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode Integer.valueOf(System.env.FGA_VERSION_CODE ?: 1)
        versionName(System.env.FGA_VERSION_NAME ?: "0.1.0")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    signingConfigs {
        debug {
            storeFile file("fgadebug.keystore")
            storePassword "123abc"
            keyAlias "fgadebug"
            keyPassword "123abc"
        }
        release {
            storeFile file("fgautomata.keystore")
            storePassword System.env.KEYSTORE_PASS
            keyAlias "fgautomata"
            keyPassword System.env.KEYSTORE_PASS
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        ci {
            initWith release
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libautomata')
    implementation project(':scripts')
    implementation project(':prefs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "androidx.activity:activity-ktx:1.2.0-alpha07"
    implementation "androidx.fragment:fragment-ktx:1.3.0-alpha07"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.github.iamareebjamal:opencv-android:4.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // ViewModel, LiveData
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation "io.mockk:mockk:1.10.2"

    implementation 'dev.ahamed.mva3:adapter:3.0.0'
    implementation 'sk.baka.slf4j:slf4j-handroid:1.7.32'

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    implementation "com.google.dagger:hilt-android:2.29.1-alpha"
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.29.1-alpha"
    kapt "com.google.dagger:hilt-compiler:2.29.1-alpha"

    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
}

kapt {
    correctErrorTypes true
}